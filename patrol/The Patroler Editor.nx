'CUSTOM DATA EDITOR FOR LOWRESNX (TIMO "INUTILIS" KLOSS)
'CDE.V8 BY MARTIN "MOECHOFE" MAUCHAUFFEE IN 2020
'URL: HTTPS://LOWRESNX.INUTILIS.COM/TOPIC.PHP?ID=1343
VERSION$="CDE.V8"

'( ) ALLOW TO HAVE A LIST OF THE SAME FIELDS TYPE
'( ) ALLOW TO REFERENCE ANOTHER RECORD, EDIT OR GOTO
'( ) UPDATE PAGELOOK TO USE GETRECORDSTRVAL

'CONFIGURATION:

GLOBAL FILETOC,FILEREC
FILETOC=5
FILEREC=6
NAMETOC$=" TOC"
NAMEREC$=" DAT"

RECSIZE=4
DIM GLOBAL RECTYPE$(25,1)

RECTYPE$(0,0)="NIL"
RECTYPE$(0,1)="NOT USED"

RECTYPE$(1,0)="ACT"
RECTYPE$(1,1)="ACTION"
RECTYPE$(2,0)="CHG"
RECTYPE$(2,1)="CHANGE"
RECTYPE$(3,0)="RES"
RECTYPE$(3,1)="RESOURCE"

DIM GLOBAL RTYPE(999)
DIM GLOBAL RNUM(999,15)
DIM GLOBAL RSTR$(999,15)
DIM RADDR(999)

DIM GLOBAL TYPESIZE(25)
DIM TYPEDATA(25,15)
DIM TYPELBL$(25,15)
DIM TYPEWDGT(25,15)
DIM TYPELINK(25,15)

TYPESIZE(0)=0

'RECORD_ACTION:
TYPESIZE(1)=1
TYPELBL$(1,0)="NAME"
TYPEDATA(1,0)=555
TYPEWDGT(1,0)=1717

'RECORD_CHANGE:
TYPESIZE(2)=2
TYPELBL$(2,0)="RESOURCE"
TYPEDATA(2,0)=888
TYPEWDGT(2,0)=66
TYPELINK(2,0)=3

TYPELBL$(2,1)="COMBO"
TYPEDATA(2,1)=777
TYPEWDGT(2,1)=1717

'RECORD_RESOURCE:
TYPESIZE(3)=1
TYPELBL$(3,0)="NAME"
TYPEDATA(3,0)=555
TYPEWDGT(3,0)=1717

DIM GLOBAL COCKTAIL$(5,1)
COCKTAIL$(0,0)="NIL"
COCKTAIL$(0,1)="NOT USED"
COCKTAIL$(1,0)="MOJITO"
COCKTAIL$(1,1)="SUGAR LIME SODA MINT RUM"
COCKTAIL$(2,0)="LIITEA"
COCKTAIL$(2,1)="VODKA TRIPLE TEQUILA RUM GIN COLA"
COCKTAIL$(3,0)="MANTAN"
COCKTAIL$(3,1)="WHISKEY VERMOUTH"
COCKTAIL$(4,0)="DAIQUI"
COCKTAIL$(4,1)="RUM LIME SUGAR AVOCADO"
COCKTAIL$(5,0)="MARITA"
COCKTAIL$(5,1)="TEQUILA TRIPLE LIME"

GLOBAL ARGRECID

SUB TYPELIST(TYPEID,FIELDID)

IF TYPEID=1 AND FIELDID=3 THEN CALL USELIST(6,COCKTAIL$())
IF TYPEID=2 AND FIELDID=1 THEN CALL USELIST(6,COCKTAIL$())
IF TYPEID=2 AND FIELDID=3 THEN CALL USELIST(6,COCKTAIL$())

END SUB
GOTO COPYPASTA

LOADRECORD:
'A=ADDRESS OF THE TEMPORARY MEMORY
'O=ADDRESS OF THE PREVIOUS RECORD
'R=RECORD ID
'T=RECORD TYPE ID
'L=NUMBER OF RECORD FIELDS
'F=RECORD FIELD ID
'D=RECORD FIELD DATA ID
RECMAX=FSIZE(FILETOC)\2-1
IF RECMAX<0 THEN RETURN
LOAD FILETOC,$A000
A=$A000
FOR R=0 TO RECMAX
CALL PEEKWORD(A,RADDR(R))
NEXT R
LOAD FILEREC,$A000
FOR R=0 TO RECMAX
A=$A000+RADDR(R)
CALL PEEKBYTE(A,RTYPE(R))
T=RTYPE(R)
L=TYPESIZE(T)
FOR F=0 TO L-1
D=TYPEDATA(T,F)
IF D=111 THEN CALL PEEKBYTE(A,RNUM(R,F))
IF D=222 THEN CALL PEEKWORD(A,RNUM(R,F))
IF D=555 THEN CALL PEEKSTR(A,RSTR$(R,F))
IF D=777 THEN CALL PEEKBYTE(A,RNUM(R,F))
NEXT F
NEXT R
RECID=0
RETURN

SUB PEEKSTR(A,V$)
L=PEEK(A)
V$=""
INC A
FOR J=1 TO L
V$=V$+CHR$(PEEK(A))
INC A
NEXT J
END SUB

SUB PEEKBYTE(A,V)
V=PEEK(A)
INC A
END SUB

SUB PEEKWORD(A,V)
V=PEEKW(A)
A=A+2
END SUB

'VARS:
COPYPASTA:

'RECID=CURRENTLY EDITED RECORD ID
'RECMAX=THE LAST AVAILABLE RECORD ID
'RECT=CURRENTLY EDITOR RECORD TYPE ID
GLOBAL RECID
RECID=-1
RECMAX=-1

'CX,CY=CELL2 TOUCH COORDS
'UX,UY=CELL3 USER FRIENDLY TOUCH COORDS
'UT=TIMER TO REPEAT THE TAP ACTION
GLOBAL CX,CY
GLOBAL UX,UY
GLOBAL UT
UT=0

'MSG$=A MESSAGE TO SHOW TO USER AT THE BOTTOM
'PMSG$=CURRENT MESSAGE DISPLAYED
'MTIME=REMAINING TIME BEFORE HIDING THE MESSAGE
'MPOS=CURRENT POSITION OF THE SLIDING MESSAGE
GLOBAL MSG$,PMSG$,MTIME,MPOS
MSG$=""

'LISTX,LISTY=X,Y ARGUMENT FOR WIDGETLST()
'LISTW=W ARGUMENT FOR WIDGETLST()
'LISTL$=L$ ARGUMENT FOR WIDGETLST()
GLOBAL LISTX,LISTY,LISTW,LISTL$

'ONLYLISTTEXT=A HACK THAT USE THE WIDGETLST TO SHOW THE VALUE ONLY
GLOBAL ONLYLISTTEXT

'MAIN:

TOUCHSCREEN
FILES
A=(INSTR(FILE$(FILETOC),VERSION$)=0 AND FSIZE(FILETOC)>0)
B=(INSTR(FILE$(FILEREC),VERSION$)=0 AND FSIZE(FILEREC)>0)
IF A OR B THEN GOSUB VERSIONMISMATCH
KEYBOARD OFF
GOSUB LOADRECORD
IF RECMAX<0 THEN GOTO PAGENEW
MSG$="RECORDS LOADED"
GOTO PAGERECORD

VERSIONMISMATCH:
CLS
PAL 4
PRINT "THOSE RECORDS WAS"
PRINT "SAVED USING A"
PRINT "DIFFERENT VERSION"
PRINT "OF CDE. CONTINUE?"
PAL 0
DO
LOCATE 0,5
INPUT "YES? ";Y$
IF Y$="Y" OR Y$="YES" THEN RETURN
WAIT VBL
LOOP

SAVERECORD:
'A=ADDRESS OF THE TEMPORARY MEMORY
'O=ADDRESS OF THE PREVIOUS RECORD
'R=RECORD ID
'T=RECORD TYPE ID
'L=NUMBER OF RECORD FIELDS
'F=RECORD FIELD ID
'D=RECORD FIELD DATA ID
CLS
A=$A000
O=$A000
FOR R=0 TO RECMAX
T=RTYPE(R)
CALL POKEBYTE(A,T)
L=TYPESIZE(T)
FOR F=0 TO L-1
D=TYPEDATA(T,F)
IF D=111 THEN CALL POKEBYTE(A,RNUM(R,F))
IF D=222 THEN CALL POKEWORD(A,RNUM(R,F))
IF D=555 THEN CALL POKESTR(A,RSTR$(R,F))
IF D=777 THEN CALL POKEBYTE(A,RNUM(R,F))
NEXT F
RADDR(R)=O-$A000
O=A
NEXT R
SAVE FILEREC,VERSION$+NAMEREC$,$A000,A-$A000
A=$A000
FOR R=0 TO RECMAX
CALL POKEWORD(A,RADDR(R))
NEXT R
SAVE FILETOC,VERSION$+NAMETOC$,$A000,A-$A000
MSG$="RECORDS SAVED"
GOTO PAGERECORD

PAGERECORD:
'T=RECORD TYPE
'F=RECORD FIELD COUNT
'I=INDEX OF THE RECORD FIELD
'D=RECORD FIELD DATA ID (111,222,555,777,888)
'X,Y=COORDINATE OF THE RECORD FIELD
'W=WIDGET SIZE
'L$=WIDGET LABEL
'M=MORE PAGE COUNTER
'YY=SCROLLING FOR THE Y AXIS
'YB=SOME WIDGET WAS NOT DISPLAYED BECAUSE THERE IS NO MORE SPACE
'YP=PREVIOUS SCROLLING FOR THE Y AXIS
'G=DO WE NEED TO GO TO ANOTHER RECORD ID
ONLYLISTTEXT=0
M=0
YY=0
YA=0
YP=1
G=-1
DO
T=-1
IF RECID>-1 THEN T=RTYPE(RECID)
WAIT VBL
CALL UPDTOUCH
CLS
IF M MOD 2=0 THEN GOSUB PAGERECORDMENU
IF M MOD 2=1 THEN GOSUB PAGERECORDMORE
IF UX=0 AND UY=0 AND RECID>0 THEN GOTO PAGERECORDPREV
IF UX=1 AND UY=0 THEN GOTO PAGERECORDNEXT
IF UX=6 AND UY=0 THEN GOSUB PAGEMORE
IF T>-1 THEN
L=TYPESIZE(T)
X=0
Y=3+YY
FOR F=0 TO L-1
D=TYPEDATA(T,F)
W=TYPEWDGT(T,F)
L$=TYPELBL$(T,F)
IF X=10 AND W=1717 THEN
Y=Y+3
X=0
END IF
YA=0
IF Y<3 THEN
ELSE IF Y>12 OR (W=4444 AND Y>9) THEN 
INC YA
IF YY<>YP THEN
MSG$="MORE FIELDS ("+STR$(YY)+")"
YP=YY
END IF
ELSE
IF D=111 THEN CALL WIDGETNUM(X,Y,W,L$,RNUM(RECID,F),255)
IF D=222 THEN CALL WIDGETNUM(X,Y,W,L$,RNUM(RECID,F),65535)
IF D=555 THEN CALL WIDGETSTR(X,Y,W,L$,RSTR$(RECID,F))
IF D=777 THEN GOSUB PAGERECORDLIST
IF D=888 THEN
CALL WIDGETTYP(X,Y,W,L$,RNUM(RECID,F),TYPELINK(T,F),G)
IF G>=0 THEN
RECID=G
GOTO PAGERECORD
END IF
END IF
END IF
IF W=66 THEN
IF X=10 THEN
Y=Y+3
X=0
ELSE
X=X+10
END IF
ELSE IF W=1717 THEN
Y=Y+3
ELSE IF W=4444 THEN
Y=Y+6
END IF
NEXT F
END IF
CALL SHOWMSG
LOOP

PAGEMORE:
INC M
IF YA>0 THEN
YY=YY-MAX(YA,2)*3
ELSE
YY=0
END IF
RETURN

PAGERECORDMENU:
PAL 0
TEXT 0,0,"PR NE    LO       MO"
TEXT 0,1,"EV XT    OK       RE"
IF RECID=RECMAX THEN
TEXT 3,0,"AD"
TEXT 3,1,"D+"
END IF
PAL 1
IF RECID>-1 THEN NUMBER 12,0,RECID,3
PAL 2
IF T>-1 THEN TEXT 12,1,LEFT$(RECTYPE$(T,0),5)
IF UX=3 AND UY=0 THEN GOTO PAGELOOK
RETURN

PAGERECORDMORE:
PAL 0
TEXT 0,0,"PR NE    TY SA    MO"
TEXT 0,1,"EV XT    PE VE    RE"
IF RECID=RECMAX THEN
TEXT 3,0,"AD"
TEXT 3,1,"D+"
END IF
IF UX=3 AND UY=0 THEN GOTO PAGERENEW
IF UX=4 AND UY=0 THEN GOTO SAVERECORD
RETURN

PAGERECORDPREV:
DEC RECID
GOTO PAGERECORD

PAGERECORDNEXT:
IF RECID=RECMAX THEN GOTO PAGENEW
INC RECID
GOTO PAGERECORD

PAGERECORDLIST:
LISTX=X
LISTY=Y
LISTW=W
LISTL$=L$
ARGRECID=RECID
CALL TYPELIST(T,F)
RETURN

SUB USELIST(A,A$())
X=LISTX
Y=LISTY
W=LISTW
L$=LISTL$
F=FIELDID
R=ARGRECID
CALL WIDGETLST(X,Y,W,L$,RNUM(R,F),A,A$())
END SUB

PAGENEW:
MSG$="NEW RECORD"
S=0
INC RECID
RTYPE(RECID)=-1
CALL CTRLLIST(RECSIZE,RECTYPE$(),RTYPE(RECID),S)
IF RTYPE(RECID)>-1 THEN
INC RECMAX
ELSE
DEC RECID
END IF
GOTO PAGERECORD

PAGERENEW:
MSG$="REUSE RECORD"
S=0
RR=-1
CALL CTRLLIST(RECSIZE,RECTYPE$(),RR,S)
IF RR>=0 THEN
T=RTYPE(RECID)
L=TYPESIZE(T)
FOR F=0 TO L-1
RNUM(RECID,F)=0
RSTR$(RECID,F)=""
NEXT F
RTYPE(RECID)=RR
END IF
GOTO PAGERECORD

PAGELOOK:
'S$=SEARCH TERM
'S=THE NUMERIC VERSION OF THE SEARCH
'D=WILL REFRESH THE RESULT
'R=RECORD ID
'R$=STRING VERSION OF THE RECORD ID
'T=RECORD TYPE ID
'O=WHEN A RECORD IS FOUND
'Y=COORDINATE OF THE FOUND RECORD
'D=DATA ID OF THE 1ST FIELD
'N=NUMBER OF RECORDS DISPLAYED
'A=START AT THIS RECORD ID
'B=TO INDIQUATE IF THERE IS MORE RECORDS TO DISPLAY
'F=RECORD FIELD ID
S$=""
D=1
A=0
B=0
CLS
DO
PAL 0
TEXT 0,0,"PR NE BA SEARCH"
TEXT 0,1,"EV XT CK"
PAGELOOKINPUT:
PAL 1
TEXT 9,1,S$+"_ "
WAIT VBL
CALL UPDTOUCH
IF UX=0 AND UY=0 AND A>0 THEN GOSUB PAGELOOKPREV
IF UX=1 AND UY=0 AND B>0 THEN GOSUB PAGELOOKNEXT
IF UX=0 AND UY>0 THEN D=1
KEYBOARD ON
K$=INKEY$
IF K$<>"" THEN
K=ASC(K$)
IF K>=32 AND K<=127 THEN S$=S$+K$
IF K=8 AND S$<>"" THEN S$=LEFT$(S$,LEN(S$)-1)
IF K=10 THEN D=1
END IF
IF UX=2 AND UY=0 THEN GOTO PAGERECORD
CALL SHOWMSG
IF D=0 THEN GOTO PAGELOOKINPUT
B=0
CLS
S=VAL(S$)
IF S=0 AND S$<>"0" THEN S=-1
Y=3
D=-1
N=0
FOR R=A TO RECMAX
IF N=4 THEN GOTO PAGELOOKNOTDONE
O=0
T=RTYPE(R)
IF TYPESIZE(T)>0 THEN D=TYPEDATA(T,0)
IF R=S THEN O=1
FOR F=0 TO TYPESIZE(T)-1
IF INSTR(RSTR$(R,F),S$)>0 THEN O=1
IF RNUM(R,F)=S AND TYPEDATA(T,F)<>777 THEN O=1
NEXT F
IF INSTR(RECTYPE$(T,0),S$)>0 OR INSTR(RECTYPE$(T,1),S$)>0 THEN O=1
IF O=1 OR S$="" THEN
PAL 0
TEXT 0,Y,"TH"
TEXT 0,Y+1,"IS"
PAL 1
NUMBER 3,Y,R,3
PAL 2
TEXT 3,Y+1,LEFT$(RECTYPE$(T,0),3)
PAL 0
IF D>=0 THEN TEXT 7,Y,LEFT$(TYPELBL$(T,0),6)
PAL 3
IF D=111 OR D=222 THEN TEXT 7,Y+1,STR$(RNUM(R,0))
IF D=555 THEN TEXT 7,Y+1,RSTR$(R,0)
IF D=777 THEN
LISTX=7
LISTY=Y+1
LISTW=66
LISTL$=""
ONLYLISTTEXT=1
CALL TYPELIST(T,0)
END IF
IF CY>=Y AND CY<=Y+1 THEN GOTO PAGELOOKTHIS
INC N
Y=Y+3
END IF
NEXT R
PAGELOOKBREAK:
D=0
LOOP
PAGELOOKNOTDONE:
B=1
GOTO PAGELOOKBREAK 
PAGELOOKPREV:
A=A-4
D=1
RETURN
PAGELOOKNEXT:
A=A+4
D=1
RETURN
PAGELOOKTHIS:
RECID=R
GOTO PAGERECORD

'CONTROL:

SUB CTRLLIST(A,A$(),V,S)
'DISPLAY A LIST OF CHOICE AND BUTTON TO SELECT IT
'CAN CHANGE TO PREVIOUS AND NEXT PAGE IF THE LIST IS TOO BIG TO
'FIT 
'A=SIZE OF THE ARRAY A$()
'A$()=ARRAY OF LIST OF CHOICE
'V=THE CURRENT SELECTED VALUE
'S=THE INDEX OF THE FIRST SHOWN ELEMENT
DO
WAIT VBL
CALL UPDTOUCH
CLS
PAL 0
TEXT 0,0,"PR NE BA FI"
TEXT 0,1,"EV XT CK ND"
E=S+3
CALL CLAMP(E,S,A-1)
FOR I=S TO E
Y=(I-S)*3+3
PAL 0
TEXT 0,Y,"TH"
TEXT 0,Y+1,"IS"
PAL 2
TEXT 3,Y,LEFT$(A$(I,0),5)
PAL 3
TEXT 3,Y+1,LEFT$(A$(I,1),17)
IF UX=0 AND CY>=Y AND CY<=Y+1 THEN
V=UY-1+S
EXIT SUB
END IF
NEXT I
IF UX=0 AND UY=0 AND S>3 THEN S=S-4
IF UX=1 AND UY=0 AND S<A-3 THEN S=S+4
IF UX=2 AND UY=0 THEN EXIT SUB
CALL SHOWMSG
LOOP
END SUB

SUB CTRLNUM(L$,V)
'L$=LABEL OF THE WIDGET
'V=THE VALUE OF THE RECORD FIELD
'V$=THE STRING VERSION OF THE VALUE
V$=""
CLS
PAL 0
TEXT 6,0,"BA       DO"
TEXT 6,1,"CK       NE"
PAL 2
TEXT 0,3,LEFT$(L$,20)
WINDOW 0,4,20,1,0
DO
WAIT VBL
CALL UPDTOUCH
CLW
PAL 3
PRINT V$;"_";
IF UX=2 AND UY=0 THEN GOTO CTRLBYTEBACK
IF UX=5 AND UY=0 THEN GOTO CTRLBYTEDONE
CALL INPUTNUM(V$)
CALL SHOWMSG
LOOP
CTRLBYTEDONE:
V=VAL(V$)
CTRLBYTEBACK:
KEYBOARD OFF
END SUB

SUB CTRLSTR(L$,O$)
'L$=LABEL OF THE WIDGET
'O$=ORIGNAL EDITED STRING VALUE
'V$=CURRENTLY EDITED STRING VALUE
V$=O$
CLS
PAL 0
TEXT 6,0,"BA       DO"
TEXT 6,1,"CK       NE"
PAL 2
TEXT 0,3,LEFT$(L$,20)
PAL 3
WINDOW 0,4,20,12,0
DO
WAIT VBL
CALL UPDTOUCH
CLW
PAL 3
PRINT V$;"_";
IF UX=2 AND UY=0 THEN GOTO CTRLSTRBACK
IF UX=5 AND UY=0 THEN GOTO CTRLSTRDONE
CALL INPUTSTR(V$)
CALL SHOWMSG
LOOP
CTRLSTRDONE:
O$=V$
CTRLSTRBACK:
KEYBOARD OFF
END SUB

SUB CTRLLINK

'S$=SEARCH TERM
'S=THE NUMERIC VERSION OF THE SEARCH
'D=WILL REFRESH THE RESULT
'R=RECORD ID
'R$=STRING VERSION OF THE RECORD ID
'T=RECORD TYPE ID
'O=WHEN A RECORD IS FOUND
'Y=COORDINATE OF THE FOUND RECORD
'D=DATA ID OF THE 1ST FIELD
'N=NUMBER OF RECORDS DISPLAYED
'A=START AT THIS RECORD ID
'B=TO INDIQUATE IF THERE IS MORE RECORDS TO DISPLAY
'F=RECORD FIELD ID
S$=""
D=1
A=0
B=0
CLS
DO
PAL 0
TEXT 0,0,"PR NE BA SEARCH"
TEXT 0,1,"EV XT CK"
WPAGELOOKINPUT:
PAL 1
TEXT 9,1,S$+"_ "
WAIT VBL
CALL UPDTOUCH
IF UX=0 AND UY=0 AND A>0 THEN GOSUB PAGELOOKPREV
IF UX=1 AND UY=0 AND B>0 THEN GOSUB PAGELOOKNEXT
IF UX=0 AND UY>0 THEN D=1
KEYBOARD ON
K$=INKEY$
IF K$<>"" THEN
K=ASC(K$)
IF K>=32 AND K<=127 THEN S$=S$+K$
IF K=8 AND S$<>"" THEN S$=LEFT$(S$,LEN(S$)-1)
IF K=10 THEN D=1
END IF
IF UX=2 AND UY=0 THEN EXIT SUB
CALL SHOWMSG
IF D=0 THEN GOTO PAGELOOKINPUT
B=0
CLS
S=VAL(S$)
IF S=0 AND S$<>"0" THEN S=-1
Y=3
D=-1
N=0
FOR R=A TO RECMAX
IF N=4 THEN GOTO PAGELOOKNOTDONE
O=0
T=RTYPE(R)
IF TYPESIZE(T)>0 THEN D=TYPEDATA(T,0)
IF R=S THEN O=1
FOR F=0 TO TYPESIZE(T)-1
IF INSTR(RSTR$(R,F),S$)>0 THEN O=1
IF RNUM(R,F)=S AND TYPEDATA(T,F)<>777 THEN O=1
NEXT F
IF INSTR(RECTYPE$(T,0),S$)>0 OR INSTR(RECTYPE$(T,1),S$)>0 THEN O=1
IF O=1 OR S$="" THEN
PAL 0
TEXT 0,Y,"TH"
TEXT 0,Y+1,"IS"
PAL 1
NUMBER 3,Y,R,3
PAL 2
TEXT 3,Y+1,LEFT$(RECTYPE$(T,0),3)
PAL 0
IF D>=0 THEN TEXT 7,Y,LEFT$(TYPELBL$(T,0),6)
PAL 3
IF D=111 OR D=222 THEN TEXT 7,Y+1,STR$(RNUM(R,0))
IF D=555 THEN TEXT 7,Y+1,RSTR$(R,0)
IF D=777 THEN
LISTX=7
LISTY=Y+1
LISTW=66
LISTL$=""
ONLYLISTTEXT=1
CALL TYPELIST(T,0)
END IF
IF CY>=Y AND CY<=Y+1 THEN GOTO PAGELOOKTHIS
INC N
Y=Y+3
END IF
NEXT R
WPAGELOOKBREAK:
D=0
LOOP
WPAGELOOKNOTDONE:
B=1
GOTO PAGELOOKBREAK 
WPAGELOOKPREV:
A=A-4
D=1
RETURN
WPAGELOOKNEXT:
A=A+4
D=1
RETURN
WPAGELOOKTHIS:
RECID=R
GOTO PAGERECORD

END SUB

'WIDGET

SUB WIDGETNUM(X,Y,W,L$,V,M)
'X,Y=COORDINATE OF THE WIDGET
'W=SIZE OF THE WIDGET (66,1717)
'WW=REAL WIDGET SIZE 
'L$=LABEL OF THE WIDGET
'V=THE VALUE OF THE RECORD FIELD
'M=MAXIMUM POSSIBLE VALUE
WW=6
IF W=1717 THEN WW=17
PAL 0
TEXT X,Y,"ED"
TEXT X,Y+1,"IT"
PAL 2
TEXT X+3,Y,LEFT$(L$,WW)
PAL 3
TEXT X+3,Y+1,STR$(V)
IF TAP AND CX>=X AND CX<=X+1 AND CY>=Y AND CY<=Y+1 THEN
CALL CTRLNUM(L$,V)
CALL CLAMP(V,0,M)
END IF
END SUB

SUB WIDGETSTR(X,Y,W,L$,V$)
'X,Y=COORDINATE OF THE WIDGET
'W=SIZE OF THE WIDGET (66,1717)
'WW=REAL WIDGET SIZE 
'L$=LABEL OF THE WIDGET
'V$=THE VALUE OF THE RECORD FIELD
'R=POSITION OF THE CARIAGE RETURN CHARACTER
'P$=CURRENTLY PRINTED TEXT FOR MULTILINE AND 4444 WIDGET
'YY=NUMBER OF ALREADY SHOWN LINES FOR MULTILINE AND 4444 WIDGET
WW=6
IF W=1717 THEN WW=17
PAL 0
TEXT X,Y,"ED"
TEXT X,Y+1,"IT"
PAL 2
TEXT X+3,Y,LEFT$(L$,WW)
PAL 3
IF W=4444 THEN
P$=V$
R=INSTR(P$,CHR$(10))
YY=0
WHILE R>0 AND YY<3
TEXT X+3,Y+1+YY,LEFT$(P$,R)
P$=RIGHT$(P$,LEN(P$)-R)
R=INSTR(P$,CHR$(10))
INC YY
WEND
IF R>0 THEN 
TEXT X+3,Y+1+YY,LEFT$(P$,R)
ELSE
TEXT X+3,Y+1+YY,LEFT$(P$,17)
END IF
ELSE
TEXT X+3,Y+1,LEFT$(V$,WW)
END IF
IF TAP AND CX>=X AND CX<=X+1 AND CY>=Y AND CY<=Y+1 THEN
CALL CTRLSTR(L$,V$)
END IF
END SUB

SUB WIDGETLST(X,Y,W,L$,V,A,A$())
'X,Y=COORDINATE OF THE WIDGET
'W=SIZE OF THE WIDGET (66,1717)
'WW=REAL WIDGET SIZE 
'L$=LABEL OF THE WIDGET
'V=THE VALUE OF THE RECORD FIELD
'A=NUMBER OF ELEMENTS IN THE LIST
'A$()=LIST OF ELEMENTS
'AA=INDEX OF THE SECOND ARGUMENT OF A$()
'S=THE INDEX OF THE FIRST SHOWN ELEMENT
S=(V\4)*4
AA=0
WW=6
IF W=1717 THEN WW=17
IF W=1717 THEN AA=1
IF ONLYLISTTEXT=1 THEN
TEXT X,Y,LEFT$(A$(V,1),W)
ELSE
PAL 0
TEXT X,Y,"ED"
TEXT X,Y+1,"IT"
PAL 2
TEXT X+3,Y,LEFT$(L$,WW)
PAL 3
TEXT X+3,Y+1,LEFT$(A$(V,AA),WW)
IF TAP AND CX>=X AND CX<=X+1 AND CY>=Y AND CY<=Y+1 THEN
CALL CTRLLIST(A,A$(),V,S)
END IF
END IF
END SUB

SUB WIDGETTYP(X,Y,W,L$,V,K,G)
'X,Y=COORDINATE OF THE WIDGET
'W=SIZE OF THE WIDGET (66,1717)
'WW=REAL WIDGET SIZE 
'L$=LABEL OF THE WIDGET
'V=THE VALUE OF THE RECORD FIELD
'V=THE RECORD ID AS VALUE FOR THE RECORD FIEL
'V$=A STRING THAT REPRESENT THE RECORD
'G=DO WE NEED TO GO TO ANOTHER RECORD ID
V$=""
CALL GETRECORDSTRVAL(V,V$)
PAL 0
TEXT X,Y,"ED GO"
TEXT X,Y+1,"IT TO"

PAL 2
TEXT X+6,Y,LEFT$(RECTYPE$(K,0),3)
PAL 1
NUMBER X+6,Y+1,V,3

IF W=1717 THEN
PAL 0
TEXT X+10,Y,LEFT$(L$,10)
PAL 3
TEXT X+10,Y+1,LEFT$(V$,10)
END IF

IF CY>=Y AND CY<=Y+1 THEN
IF UX=1 THEN G=V
IF UX=0 THEN TRACE "EDIT"
END IF

END SUB

'UTIL:

SUB GETRECORDSTRVAL(R,V$)
T=RTYPE(R)
L=TYPESIZE(T)
FOR F=0 TO L-1
V$=RSTR$(R,F)
IF V$<>"" THEN EXIT SUB
NEXT F
END SUB

SUB UPDTOUCH
CX=TOUCH.X\8
CY=TOUCH.Y\8
UX=-1
UY=-1
IF UT>0 THEN DEC UT
IF UT<0 THEN INC UT
IF TAP THEN UT=25
IF (TAP AND (CX MOD 3)<2 AND (CY MOD 3)<2) OR (TOUCH AND UT=0) THEN
UX=CX\3
UY=CY\3
IF NOT TAP THEN UT=-5
END IF
END SUB

SUB SHOWMSG
IF MSG$="" THEN
EXIT SUB
ELSE IF MSG$<>PMSG$ THEN
MPOS=0
MTIME=70+LEN(MSG$)*2
PMSG$=MSG$
ELSE IF MTIME>0 AND MPOS<LEN(MSG$) THEN
INC MPOS
ELSE IF MTIME>0 THEN
DEC MTIME
ELSE IF MPOS>0 THEN
DEC MPOS
ELSE IF MPOS=0 THEN
MSG$=""
PMSG$=""
EXIT SUB
END IF
ATTR 4
TEXT 0,15,RIGHT$(MSG$,MPOS)+" "
END SUB

SUB CLAMP(V,I,A)
V=MAX(I,MIN(A,V))
END SUB

SUB INPUTNUM(V$)
'V$=THE INPUT VALUE TO MODIFY
KEYBOARD ON
K$=INKEY$
IF K$<>"" THEN
K=ASC(K$)
IF K>=48 AND K<=57 THEN V$=LEFT$(V$+K$,19)
IF K=8 AND V$<>"" THEN V$=LEFT$(V$,LEN(V$)-1)
END IF
END SUB

SUB INPUTSTR(V$)
'V$=THE INPUT VALUE TO MODIFY
KEYBOARD ON
K$=INKEY$
IF K$<>"" THEN
K=ASC(K$)
IF K>=32 AND K<=127 THEN V$=V$+K$
IF K=8 AND V$<>"" THEN V$=LEFT$(V$,LEN(V$)-1)
IF K=10 THEN V$=V$+CHR$(10)
END IF
END SUB

SUB POKESTR(A,V$)
L=MIN(255,LEN(V$))
POKE A,L
INC A
FOR J=1 TO L
POKE A,ASC(MID$(V$,J,1))
INC A
NEXT J
END SUB

SUB POKEBYTE(A,V)
POKE A,V
INC A
END SUB

SUB POKEWORD(A,V)
POKEW A,V
A=A+2
END SUB

#1:MAIN PALETTES
003F2A15002F1B02003E2914003A3420
00353010003F2A15003F2A15003F2A15

#5:CDE.V8 TOC
00000F00

#6:CDE.V8 DAT
030D43484F434F4C4F54313233343502

